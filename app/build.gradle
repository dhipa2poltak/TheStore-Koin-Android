plugins {
  id 'com.android.application'
  id 'org.jetbrains.kotlin.android'
}

android {
  compileSdk ConfigData.compileSdkVersion

  defaultConfig {
    applicationId "com.dpfht.thestore_koin"
    minSdk ConfigData.minSdkVersion
    targetSdk ConfigData.targetSdkVersion
    versionCode 1
    versionName "1.0"

    multiDexEnabled true

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  }

  buildTypes {
    release {
      minifyEnabled true
      shrinkResources true
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

      manifestPlaceholders.appNameSuffix = ""
    }
    debug{
      applicationIdSuffix ".debug"
      versionNameSuffix "-debug"

      manifestPlaceholders.appNameSuffix = " (debug)"
    }
  }
  buildFeatures {
    viewBinding true
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  kotlinOptions {
    jvmTarget = '1.8'
  }

  flavorDimensions "default"

  productFlavors {
    staging {
      applicationId "com.dpfht.thestore_koin.staging"
      manifestPlaceholders.appName = "TheStore-Koin-Staging"
    }
    verify {
      applicationId "com.dpfht.thestore_koin.verify"
      manifestPlaceholders.appName = "TheStore-Koin-Verify"
    }
    prod {
      applicationId "com.dpfht.thestore_koin"
      manifestPlaceholders.appName = "TheStore-Koin"
    }
  }
}

dependencies {

  implementation project(":framework")
  implementation project(":features:feature_splash")
  implementation project(":features:feature_error_message")
  implementation project(":features:feature_list")
  implementation project(":features:feature_details")

  implementation Deps.coreKtx
  implementation Deps.appCompat
  implementation Deps.material
  implementation Deps.constraintLayout
  testImplementation Deps.jUnit
  androidTestImplementation Deps.jUnitExt
  androidTestImplementation Deps.espresso

  implementation Deps.navigationFragment
  implementation Deps.navigationUI

  implementation Deps.multiDex

  implementation Deps.koin
}
